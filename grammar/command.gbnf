root ::= command newline

command ::= say | think | save-memory | recall-memory

say ::= "say" space message
think ::= "think" space message
save-memory ::= "save_memory" space message
recall-memory ::= "recall_memory" space message

message ::= quote text quote
text ::= sentence (space sentence)*

sentence-join ::= (", " | "; ")
sentence-end ::= ("." | "!" | "?" | "...")
word-join ::= ("'" | "-")
number ::= [0-9]+

lowercase-word ::=       [a-z]+ (word-join [a-z]+)?
titlecase-word ::= [A-Z] [a-z]* (word-join [a-z]+)?
word ::= titlecase-word | lowercase-word | number

word-string ::= word (space word)*
sentence ::= word-string (sentence-join word-string)* sentence-end

newline ::= "\n"
space ::= " "
quote ::= "\""
